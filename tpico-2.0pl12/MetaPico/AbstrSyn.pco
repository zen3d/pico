`*-----------------------------------*`
`*           >>>Pico 2.0<<<          *`
`*            Theo D'Hondt           *`
`*  VUB Programming Technology Lab   *`
`*               ©2002               *`
`*-----------------------------------*`
`*          abstract syntax          *`
`*       (METACIRCULAR VERSION)      *`
`*-----------------------------------*`

`---------------------------------------------------------------------`
` <expression>    ::= <void>                                          `
` <expression>    ::= <reference>                                     `
` <expression>    ::= <application>                                   `
` <expression>    ::= <tabulation>                                    `
` <expression>    ::= <qualification>                                 `
` <expression>    ::= <declaration>                                   `
` <expression>    ::= <definition>                                    `
` <expression>    ::= <assignment>                                    `
` <expression>    ::= <constant>                                      `
` <expression>    ::= <variable>                                      `
` <expression>    ::= <continuation>                                  `
` <expression>    ::= <native>                                        `
` <expression>    ::= <function>                                      `
` <expression>    ::= <table>                                         `
` <expression>    ::= <text>                                          `
` <expression>    ::= <fraction>                                      `
` <expression>    ::= <quotation>                                     `
` <expression>    ::= <number>                                        `
`                                                                     `
` <void>          ::= VOI                                             `
` <reference>     ::= REF <name>                                      `
` <application>   ::= APL <expression> <arguments>                    `
` <tabulation>    ::= TBL <expression> <indexation>                   `
` <qualification> ::= QUA <expression> <invocation>                   `
` <declaration>   ::= DCL <invocation> <expression>                   `
` <definition>    ::= DEF <invocation> <expression>                   `
` <assignment>    ::= SET <invocation> <expression>                   `
` <constant>      ::= CST <name> <expression> <dictionary>            `
` <variable>      ::= VAR <name> <expression> <dictionary>            `
` <continuation>  ::= CNT <dictionary> <number> <number> <table>      `
` <native>        ::= NAT <name> <number>                             `
` <function>      ::= FUN <name> <arguments> <expression> <dictionary>`
` <table>         ::= TAB <table>                                     `
` <text>          ::= TXT <text>                                      `
` <fraction>      ::= FRC <fraction>                                  `
` <quotation>     ::= QUO <expression>                                `
` <number>        ::= NBR <number>                                    `
`                                                                     `
` <name>          ::= <text>                                          `
`                                                                     `
` <indexation>    ::= <table>                                         `
`                                                                     `
` <arguments>     ::= <table>                                         `
` <arguments>     ::= <invocation>                                    `
`                                                                     `
` <dictionary>    ::= <variable>                                      `
` <dictionary>    ::= <constant>                                      `
` <dictionary>    ::= <void>                                          `
`                                                                     `
` <invocation>    ::= <reference>                                     `
` <invocation>    ::= <application>                                   `
` <invocation>    ::= <tabulation>                                    `
`                                                                     `
`---------------------------------------------------------------------`

